; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "OpenMoBu for Autodesk® MotionBuilder®"
#define MyAppVersion "0.5a"
#define MyAppPublisher "Neill3d"
#define MyAppURL "https://github.com/Neill3d/MoBu"
#define MyAppExeName "Setup_OpenMoBu_05a"

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"; InfoBeforeFile:"infoBefore_en.txt"; InfoAfterFile: "infoAfter_en.txt"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"; InfoBeforeFile:"infoBefore_ru.txt"; InfoAfterFile: "infoAfter_ru.txt"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2A0E1623-47E7-49DD-87AA-0BAB1C1226B0}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
CreateAppDir=no
OutputBaseFilename={#MyAppExeName}
Compression=lzma
SolidCompression=yes
AppCopyright=Copyright (C) 2018 Neill3d
;WizardImageFile=Neill3d2.jpg
WizardImageStretch=no
;WizardSmallImageFile=Neill3d2.jpg
DisableWelcomePage=no

[Types]
Name: "full"; Description: "Full installation"
Name: "2017"; Description: "MotionBuilder 2017"
Name: "2018"; Description: "MotionBuilder 2018"

Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Components]
Name: "MotionBuilder2017"; Description: "MotionBuilder 2017 plugins"; Types: full 2017 custom;
Name: "MotionBuilder2018"; Description: "MotionBuilder 2018 plugins"; Types: full 2018 custom;

[Files]

; 2017
Source: "..\bin\x64\plugins_2017\manip_lockcamera.dll"; DestDir: "{code:MoBu_Path64|2017}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2017

; 2018
Source: "..\bin\x64\plugins_2018\manip_lockcamera.dll"; DestDir: "{code:MoBu_Path64|2018}\bin\x64\plugins\"; Flags: ignoreversion; Components: MotionBuilder2018


; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]

function MoBu_Path32(Param: String): String;
var
  key, path: String;
begin
  key := 'Software\Autodesk\MotionBuilder\' + Param;
  Result := '';
  if RegKeyExists(HKEY_LOCAL_MACHINE, key) then
  begin
    If RegQueryStringValue(HKEY_LOCAL_MACHINE, key, 'InstallPath', path) then
    begin
      Result := path;
    end;
  end;
end;

function MoBu_Path64(Param: String): String;
var
  key, path: String;
begin
  key := 'Software\Autodesk\MotionBuilder\' + Param;
  Result := '';
  if IsWin64 and RegKeyExists(HKLM64, key) then
  begin
    If RegQueryStringValue(HKLM64, key, 'InstallPath', path) then
    begin
      Result := path;
    end;
  end;
end;

function IsMoBu(Param: String): Boolean;
begin
  Result := False;
  if RegKeyExists(HKLM, 'Software\Autodesk\MotionBuilder\' + Param) then
  begin
    Result := True;
  end;
end;

function IsMoBu64(Param: String) : Boolean;
begin
  Result := False;
  if IsWin64 and RegKeyExists(HKLM64, 'Software\Autodesk\MotionBuilder\' + Param) then
  begin
    Result := True;
  end;
end;